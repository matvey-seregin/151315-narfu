#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	ЗаписатьФотоДоговора(ТекущийОбъект);

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПоказатьФотоДоговора(ТекущийОбъект);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФотографииЗаказов.НомерФотографии КАК НомерФото,
		|	ФотографииЗаказов.Подпись КАК Подпись
		|ИЗ
		|	РегистрСведений.ФотографииЗаказов КАК ФотографииЗаказов
		|ГДЕ
		|	ФотографииЗаказов.Заказ = &Заказ";
	Запрос.УстановитьПараметр("Заказ", Объект.Ссылка);
	
	СписокФото.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОтобразитьКартинку(0);
	
	Если ЗначениеЗаполнено(Объект.ДатаНачалаРаботы) И ЗначениеЗаполнено(Объект.ДатаОкончанияРаботы) Тогда
		Элементы.НачатьРаботу.Заголовок = "Работа завершена";
		Элементы.НачатьРаботу.Доступность = Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ДатаНачалаРаботы) И  НЕ ЗначениеЗаполнено(Объект.ДатаОкончанияРаботы) Тогда
		Элементы.НачатьРаботу.Заголовок = "ЗакончитьРаботу";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Элементы.СовершитьЗвонок.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Элементы.СовершитьЗвонок.Доступность = Истина;
	Иначе
		Элементы.СовершитьЗвонок.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы



&НаКлиенте
Процедура КартинкаВперед(Команда)
	ОтобразитьКартинку(ТекущаяКартинка + 1);
КонецПроцедуры

&НаКлиенте
Процедура КартинкаНазад(Команда)
	ОтобразитьКартинку(ТекущаяКартинка - 1);
КонецПроцедуры

&НаКлиенте
Асинх Процедура СделатьФото(Команда)
	
	#Если Не МобильноеПриложениеКлиент Тогда
		ПредупреждениеАсинх("Прикрепление фото возможно только в мобильном приложении");
		Возврат;
	#КонецЕсли	
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Ответ = Ждать ВопросАсинх(
			"Для прикрепления фото документ будет записан. Продолжить?",
			РежимДиалогаВопрос.ДаНет);
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
		
	Если Не Записать() Тогда
		Возврат;
	КонецЕсли;		
	
	#Если МобильноеПриложениеКлиент Тогда
		
		Если НЕ СредстваМультимедиа.ПоддерживаетсяФотоснимок() Тогда
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = "Данное устройство не поддерживает работу с камерой";
			Сообщение.Сообщить();
			Возврат;
		КонецЕсли;
		
		ДанныеМультимедиа = СредстваМультимедиа.СделатьФотоснимок();
		Если ДанныеМультимедиа = Неопределено Тогда
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = "Не удалось сделать фото";
			Сообщение.Сообщить();
			Возврат;     
		КонецЕсли;
		
		ДвоичныеДанныеФото = ДанныеМультимедиа.ПолучитьДвоичныеДанные();
		
		//@skip-check use-non-recommended-method - на клиенте доступен только этот метод
		Подпись = Ждать ВвестиСтрокуАсинх(
		Формат(ТекущаяДата(), "ДФ='""Фото от ""дд.ММ.гггг ЧЧ:мм:сс';"),
		"Подпись к фотографии");
		
		ДанныеФото = ЗаписатьФотоВРегистр(Объект.Ссылка, ДвоичныеДанныеФото, Подпись);
		
		Строка = СписокФото.Добавить();
		Строка.НомерФото = ДанныеФото.НомерФотографии;
		Строка.Подпись = ДанныеФото.Подпись;
		
		ОтобразитьКартинку(СписокФото.Количество() - 1);
		
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьГеопозицию(Команда)
	
	#Если МобильноеПриложениеКлиент Тогда
	Провайдеры  = СредстваГеопозиционирования.ПолучитьПровайдеров();
	Для Каждого Провайдер Из Провайдеры Цикл
		Если НЕ Провайдер.Платный Тогда
			ИскомыйПровайдер = Провайдер;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ИскомыйПровайдер = Неопределено Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "ru = 'Требуемый провайдер не обнаружен'";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	ДанныеМестоположения = СредстваГеопозиционирования.ПолучитьПоследнееМестоположение(ИскомыйПровайдер.Имя);
	Адрес = ПолучитьАдресПоМестоположению(ДанныеМестоположения.Координаты);

	Объект.ГеоПозиция = СтрШаблон("%1, %2 %3", Адрес.Город, Адрес.Улица, Адрес.Дом);
	
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьРаботу(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.ДатаНачалаРаботы) Тогда
		Объект.ДатаНачалаРаботы = ТекущаяДата();
		Элементы.НачатьРаботу.Заголовок = "Закончить работу";
	Иначе
		Объект.ДатаОкончанияРаботы = ТекущаяДата();
		Элементы.НачатьРаботу.Заголовок = "Работа завершена";
		Элементы.НачатьРаботу.Доступность = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СовершитьЗвонок(Команда)
	#Если МобильноеПриложениеКлиент Тогда
	Если СредстваТелефонии.ПоддерживаетсяНаборНомера() Тогда	
		 НомерТелефона = ПолучитьТелефонКонтрагентаНаСервере(Объект.Контрагент);
		СредстваТелефонии.НабратьНомер(НомерТелефона, Истина);
	КонецЕсли;
	#КонецЕсли
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьТелефонКонтрагентаНаСервере(Контрагент)
	НомерТелефона = Контрагент.НомерТелефона;
	Возврат НомерТелефона;
КонецФункции

&НаКлиенте
Процедура ОтобразитьКартинку(Знач ИндексКартинки)
	
	Если СписокФото.Количество() - 1 < ИндексКартинки Тогда
		ИндексКартинки = СписокФото.Количество() - 1;
	КонецЕсли;
	
	Если ИндексКартинки = -1 Тогда
		Картинка = "";
		Элементы.КартинкаВперед.Доступность = Ложь;
		Элементы.КартинкаНазад.Доступность = Ложь;
		Возврат;
	КонецЕсли;
	
	Элементы.КартинкаВперед.Доступность = Истина;
	Элементы.КартинкаНазад.Доступность = Истина;
	
	Строка = СписокФото[ИндексКартинки];
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Заказ", Объект.Ссылка);
	ЗначенияЗаполнения.Вставить("НомерФотографии", Строка.НомерФото);
	
	ПараметрыКонструктора = Новый Массив;
	ПараметрыКонструктора.Добавить(ЗначенияЗаполнения);
	
	Ключ = Новый(Тип("РегистрСведенийКлючЗаписи.ФотографииЗаказов"), ПараметрыКонструктора);
	
	Картинка = ПолучитьНавигационнуюСсылку(Ключ, "ДанныеФото");
	
	Если ИндексКартинки = 0 Тогда
		Элементы.КартинкаНазад.Доступность = Ложь;
	КонецЕсли;
	
	Если ИндексКартинки = СписокФото.Количество() - 1 Тогда
		Элементы.КартинкаВперед.Доступность = Ложь;
	КонецЕсли;
	
	ТекущаяКартинка = ИндексКартинки;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаписатьФотоВРегистр(Ссылка, ДвоичныеДанныеФото, Подпись)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЕСТЬNULL(МАКСИМУМ(ФотографииЗаказов.НомерФотографии), 0) КАК НомерФотографии
		|ИЗ
		|	РегистрСведений.ФотографииЗаказов КАК ФотографииЗаказов
		|ГДЕ
		|	ФотографииЗаказов.Заказ = &Заказ";
	Запрос.УстановитьПараметр("Заказ", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	
	НомерНовойФотографии = Выборка.НомерФотографии + 1;
	
	Запись = РегистрыСведений.ФотографииЗаказов.СоздатьМенеджерЗаписи();
	Запись.Заказ = Ссылка;
	Запись.НомерФотографии = НомерНовойФотографии;
	Запись.ДанныеФото = Новый ХранилищеЗначения(ДвоичныеДанныеФото);
	Запись.Подпись = Подпись;
	Запись.Записать();
	
	Результат = Новый Структура;
	Результат.Вставить("НомерФотографии", НомерНовойФотографии);
	Результат.Вставить("Подпись", Подпись);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗаписатьФотоДоговора(ТекущийОбъект)
	
	Если ЭтоАдресВременногоХранилища(Картинка) Тогда
		ТекущийОбъект.ФотоДоговора = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(Картинка));
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПоказатьФотоДоговора(ТекущийОбъект)
	
	Картинка = ПолучитьНавигационнуюСсылку(ТекущийОбъект.Ссылка, "ФотоДоговора");
	
КонецПроцедуры

#КонецОбласти



