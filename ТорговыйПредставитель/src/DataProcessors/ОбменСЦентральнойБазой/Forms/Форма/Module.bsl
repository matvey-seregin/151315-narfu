
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Адрес = Константы.АдресЦентральнойБазы.Получить();
	Логин = Константы.ЛогинЦентральнойБазы.Получить();
	Пароль = Константы.ПарольЦентральнойБазы.Получить();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПарольПриИзменении(Элемент)
	СохранитьНастройки();
КонецПроцедуры

&НаКлиенте
Процедура ЛогинПриИзменении(Элемент)
	СохранитьНастройки();
КонецПроцедуры

&НаКлиенте
Процедура АдресПриИзменении(Элемент)
	СохранитьНастройки();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьОбмен(Команда)
	ВыполнитьОбменНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьДанныеЗаказов(Команда)
	ОтправитьДанныеЗаказовНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьНастройки()
	
	Константы.АдресЦентральнойБазы.Установить(Адрес);
	Константы.ЛогинЦентральнойБазы.Установить(Логин);
	Константы.ПарольЦентральнойБазы.Установить(Пароль);
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьОбменНаСервере()
	
	НастройкиПодключения = НастройкиПодключения();
	
	Соединение = Новый HTTPСоединение(НастройкиПодключения.АдресСервера,,
		НастройкиПодключения.Логин,
		НастройкиПодключения.Пароль,,,
		НастройкиПодключения.ЗащищенноеСоединение);
		
	СистемнаяИнформации = Новый СистемнаяИнформация;
	ИдентификаторКлиента = Строка(СистемнаяИнформации.ИдентификаторКлиента);
	
	ЭтотУзел = ПланыОбмена.ОбменСЦентральнойБазой.ЭтотУзел();
	НомерПринятого = НомерПринятогоСообщения(ЭтотУзел);
		
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("X-MobileID", ИдентификаторКлиента);
	Заголовки.Вставить("X-Received-Message", НомерПринятого);
	
	АдресРесурса = НастройкиПодключения.ПутьНаСервере + "/hs/mobile/exchange";
	Запрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	
	//@skip-check bsl-legacy-check-dynamic-feature-access - устарел только на клиенте
	Ответ = Соединение.Получить(Запрос);
	
	Если Ответ.КодСостояния <> 200 Тогда
		ВызватьИсключение "Ошибка обмена";
	КонецЕсли;
	
	ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
	ДанныеОтвета = ПрочитатьЗначениеJSON(ТелоОтвета);
	
	НомерПринятого = ДанныеОтвета.НомерСообщения;
	
	Для Каждого ОписаниеОбъекта Из ДанныеОтвета.ИзмененныеОбъекты Цикл
		
		Если ОписаниеОбъекта.Тип = "Справочник.ДоговорыКонтрагентов" Тогда
			ЗаписатьДоговор(ОписаниеОбъекта);
		ИначеЕсли ОписаниеОбъекта.Тип = "Справочник.Контрагенты" Тогда
			ЗаписатьКонтрагента(ОписаниеОбъекта);
		ИначеЕсли ОписаниеОбъекта.Тип = "Справочник.Номенклатура" Тогда 
			ЗаписатьНоменклатуру(ОписаниеОбъекта);
		Иначе
			Продолжить;
		КонецЕсли;
		
	КонецЦикла;
	
	УзелОбъект = ЭтотУзел.ПолучитьОбъект();
	УзелОбъект.ОбменДанными.Загрузка = Истина;
	УзелОбъект.ЭтотУзел = Ложь;
	УзелОбъект.НомерПринятого = НомерПринятого;
	УзелОбъект.ЭтотУзел = Истина;
	УзелОбъект.Записать();
	
КонецПроцедуры

&НаСервере
Функция НомерПринятогоСообщения(Узел)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОбменСЦентральнойБазой.НомерПринятого КАК НомерПринятого
		|ИЗ
		|	ПланОбмена.ОбменСЦентральнойБазой КАК ОбменСЦентральнойБазой
		|ГДЕ
		|	ОбменСЦентральнойБазой.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Узел);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.НомерПринятого;
	
КонецФункции

&НаСервере
Процедура ОтправитьДанныеЗаказовНаСервере()
    
    НастройкиПодключения = НастройкиПодключения();

	Соединение = Новый HTTPСоединение(НастройкиПодключения.АдресСервера, , НастройкиПодключения.Логин,
		НастройкиПодключения.Пароль, , , НастройкиПодключения.ЗащищенноеСоединение);
    
    ЭтотУзел = ПланыОбмена.ОбменСЦентральнойБазой.ЭтотУзел();
    НомерОтправленного = НомерОтправленного(ЭтотУзел);
    
	Узел = ПланыОбмена.ОбменСЦентральнойБазой.НайтиПоКоду("ЦБ");
	Если Не ЗначениеЗаполнено(Узел)	Тогда
		УзелОбъект = ПланыОбмена.ОбменСЦентральнойБазой.СоздатьУзел();
		УзелОбъект.Код = "ЦБ";
		УзелОбъект.Записать();
		Узел = УзелОбъект.Ссылка;
		ПервичнаяРегистрация(Узел);
	КонецЕсли;
    
    ПланыОбмена.УдалитьРегистрациюИзменений(Узел, НомерОтправленного);
    
    НомерСообщения = НомерОтправленного + 1;
    
    ДанныеДляОтправки = ПолучитьДанныеЗаказов(Узел, НомерСообщения);
    
    JSONДанные = ЗаписатьЗначениеJSON(ДанныеДляОтправки);

    Заголовки = Новый Соответствие;
    Заголовки.Вставить("Content-Type", "application/json");
    
    АдресРесурса = НастройкиПодключения.ПутьНаСервере + "/hs/mobile/receive";
    Запрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
    Запрос.УстановитьТелоИзСтроки(JSONДанные);
    
    Попытка
        //@skip-check bsl-legacy-check-dynamic-feature-access
        Ответ = Соединение.ОтправитьДляОбработки(Запрос);
        
        Если Ответ.КодСостояния = 200 Тогда
        	
            Сообщение = Новый СообщениеПользователю;
            Сообщение.Текст = "Данные успешно отправлены на сервер!";
            Сообщение.Сообщить();
 	
			УзелОбъект = ЭтотУзел.ПолучитьОбъект();
			УзелОбъект.ОбменДанными.Загрузка = Истина;
			УзелОбъект.ЭтотУзел = Ложь;
			УзелОбъект.НомерОтправленного = НомерОтправленного;
			УзелОбъект.ЭтотУзел = Истина;
			УзелОбъект.Записать();
            
        Иначе
            ВызватьИсключение "Ошибка отправки данных. Код: " + Ответ.КодСостояния + 
            	"Тело ответа: " + Ответ.ПолучитьТелоКакСтроку();
        КонецЕсли;
        
    Исключение
        Сообщение = Новый СообщениеПользователю;
        Сообщение.Текст = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
        Сообщение.Сообщить();
    КонецПопытки;
    
КонецПроцедуры

&НаСервере
Функция НомерОтправленного(ЭтотУзел)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОбменСЦентральнойБазой.НомерОтправленного КАК НомерОтправленного
		|ИЗ
		|	ПланОбмена.ОбменСЦентральнойБазой КАК ОбменСЦентральнойБазой
		|ГДЕ
		|	ОбменСЦентральнойБазой.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", ЭтотУзел);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.НомерОтправленного;
	
КонецФункции	

&НаСервере
Функция ПолучитьДанныеЗаказов(Узел, НомерСообщения)
	
	ВыборкаИзменений = ПланыОбмена.ВыбратьИзменения(Узел, НомерСообщения);
	
	МассивДанных = Новый Массив;
		
	Пока ВыборкаИзменений.Следующий() Цикл
		ДокОбъект = ВыборкаИзменений.Получить();
		Если ТипЗнч(ДокОбъект) = Тип("ДокументОбъект.ЗаказПокупателя") Тогда
			ДанныеДокумента = СформироватьМассивДанных(ДокОбъект);
			МассивДанных.Добавить(ДанныеДокумента);
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивДанных;
	
КонецФункции

&НаСервере
Процедура ПервичнаяРегистрация(Узел)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаказПокупателя.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗаказПокупателя КАК ЗаказПокупателя";

	Выборка = Запрос.Выполнить().Выбрать();
	
	ДанныеКРегистрации = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		ДанныеКРегистрации.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ДанныеКРегистрации) Тогда
		ПланыОбмена.ЗарегистрироватьИзменения(Узел, ДанныеКРегистрации);
	КонецЕсли;	

КонецПроцедуры

&НаСервере
Функция СформироватьМассивДанных(Документ)
    
    ДанныеДокумента = Новый Структура;
    
    ДанныеДокумента.Вставить("Тип", "Документ.ЗаказПокупателя");
    ДанныеДокумента.Вставить("Дата", Формат(Документ.Дата, "ДФ=yyyyMMddHHmmss"));
    ДанныеДокумента.Вставить("Номер", Документ.Номер);
    ДанныеДокумента.Вставить("Комментарий", Документ.Комментарий);
    ДанныеДокумента.Вставить("Проведен", Документ.Проведен);
    ДанныеДокумента.Вставить("Ссылка", Строка(Документ.Ссылка.УникальныйИдентификатор()));
    ДанныеДокумента.Вставить("Геопозиция", Документ.ГеоПозиция);
	ДанныеДокумента.Вставить("ДатаНачалаРаботы", Формат(Документ.ДатаНачалаРаботы, "ДФ=yyyyMMddHHmmss"));
    ДанныеДокумента.Вставить("ДатаОкончанияРаботы", Формат(Документ.ДатаОкончанияРаботы, "ДФ=yyyyMMddHHmmss"));
    
    Если Не Документ.Контрагент.Пустая() Тогда
        ДанныеДокумента.Вставить("Контрагент", Строка(Документ.Контрагент.УникальныйИдентификатор()));
    Иначе
        ДанныеДокумента.Вставить("Контрагент", "");
    КонецЕсли;
    
    Если Не Документ.Договор.Пустая() Тогда
        ДанныеДокумента.Вставить("Договор", Строка(Документ.Договор.УникальныйИдентификатор()));
    Иначе
        ДанныеДокумента.Вставить("Договор", "");
    КонецЕсли;
    
    Если Документ.СписокНоменклатуры.Количество() > 0 Тогда
        МассивНоменклатуры = Новый Массив;
        
        Для Каждого СтрокаНоменклатуры Из Документ.СписокНоменклатуры Цикл
            ДанныеСтроки = Новый Структура;
            
            Если Не СтрокаНоменклатуры.Номенклатура.Пустая() Тогда
                ДанныеСтроки.Вставить("Номенклатура", Строка(СтрокаНоменклатуры.Номенклатура.УникальныйИдентификатор()));
            Иначе
                ДанныеСтроки.Вставить("Номенклатура", "");
            КонецЕсли;
            
            // Количество
            ДанныеСтроки.Вставить("Количество", СтрокаНоменклатуры.Количество);
            
            МассивНоменклатуры.Добавить(ДанныеСтроки);
        КонецЦикла;
        
        ДанныеДокумента.Вставить("СписокНоменклатуры", МассивНоменклатуры);
    Иначе
        ДанныеДокумента.Вставить("СписокНоменклатуры", Новый Массив);
    КонецЕсли;
    
    Если ЗначениеЗаполнено(Документ.ФотоДоговора) Тогда
        Попытка
            ДвоичныеДанные = Документ.ФотоДоговора.Получить();
            Если ДвоичныеДанные <> Неопределено Тогда
                ДанныеДокумента.Вставить("ФотоДоговораBase64", Base64Строка(ДвоичныеДанные));
            Иначе
                ДанныеДокумента.Вставить("ФотоДоговораBase64", "");
            КонецЕсли;
        Исключение
            СообщениеПользователю = Новый СообщениеПользователю;
            СообщениеПользователю.Текст = "Не удалось получить фото договора: " + ОписаниеОшибки();
            СообщениеПользователю.Сообщить();
            ДанныеДокумента.Вставить("ФотоДоговораBase64", "");
        КонецПопытки;
    Иначе
        ДанныеДокумента.Вставить("ФотоДоговораBase64", "");
    КонецЕсли;

    
    Возврат ДанныеДокумента;
    
КонецФункции

&НаСервере
Процедура ЗаписатьНоменклатуру(ОписаниеОбъекта)
	
	Ссылка = Справочники.Номенклатура.ПолучитьСсылку(
		Новый УникальныйИдентификатор(ОписаниеОбъекта.Идентификатор));
		
	СпрОбъект = Ссылка.ПолучитьОбъект();
	Если СпрОбъект = Неопределено Тогда
		Если ОписаниеОбъекта.ЭтоГруппа Тогда
			СпрОбъект = Справочники.Номенклатура.СоздатьГруппу();
		Иначе
			СпрОбъект = Справочники.Номенклатура.СоздатьЭлемент();
		КонецЕсли;
		СпрОбъект.УстановитьСсылкуНового(Ссылка);
	КонецЕсли;
	
	СпрОбъект.Наименование = ОписаниеОбъекта.Наименование;
	СпрОбъект.Родитель = Справочники.Номенклатура.ПолучитьСсылку(
		Новый УникальныйИдентификатор(ОписаниеОбъекта.Родитель));
		
	СпрОбъект.ОбменДанными.Загрузка = Истина;
	
	СпрОбъект.Записать();

КонецПроцедуры

&НаСервере
Процедура ЗаписатьКонтрагента(ОписаниеОбъекта)
	
	Ссылка = Справочники.Контрагенты.ПолучитьСсылку(
		Новый УникальныйИдентификатор(ОписаниеОбъекта.Идентификатор));
		
	СпрОбъект = Ссылка.ПолучитьОбъект();
	Если СпрОбъект = Неопределено Тогда
		СпрОбъект = Справочники.Контрагенты.СоздатьЭлемент();
		СпрОбъект.УстановитьСсылкуНового(Ссылка);
	КонецЕсли;
	
	СпрОбъект.Наименование = ОписаниеОбъекта.Наименование;
	СпрОбъект.НомерТелефона = ОписаниеОбъекта.НомерТелефона;
		
	СпрОбъект.ОбменДанными.Загрузка = Истина;
	
	СпрОбъект.Записать();

КонецПроцедуры

&НаСервере
Процедура ЗаписатьДоговор(ОписаниеОбъекта)
	
	Ссылка = Справочники.ДоговорыКонтрагентов.ПолучитьСсылку(
		Новый УникальныйИдентификатор(ОписаниеОбъекта.Идентификатор));
		
	СпрОбъект = Ссылка.ПолучитьОбъект();
	Если СпрОбъект = Неопределено Тогда
		СпрОбъект = Справочники.ДоговорыКонтрагентов.СоздатьЭлемент();
		СпрОбъект.УстановитьСсылкуНового(Ссылка);
	КонецЕсли;
	
	СпрОбъект.Наименование = ОписаниеОбъекта.Наименование;
	СпрОбъект.Владелец = Справочники.Контрагенты.ПолучитьСсылку(
		Новый УникальныйИдентификатор(ОписаниеОбъекта.Владелец));
		
	СпрОбъект.ОбменДанными.Загрузка = Истина;
	
	СпрОбъект.Записать();

КонецПроцедуры

&НаСервере
Функция НастройкиПодключения()
	
	Результат = Новый Структура;
	Результат.Вставить("Логин", Логин);
	Результат.Вставить("Пароль", Пароль);
	
	Если СтрНачинаетсяС(Адрес, "https") Тогда
		ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL();
	Иначе
		ЗащищенноеСоединение = Неопределено;
	КонецЕсли;		
		
	Результат.Вставить("ЗащищенноеСоединение", ЗащищенноеСоединение);
	
	Курсор = СтрНайти(Адрес, "://");
	АдресБезПротокола = Сред(Адрес, Курсор + 3);
	
	Курсор = СтрНайти(АдресБезПротокола, "/");
	АдресСервера = Лев(АдресБезПротокола, Курсор - 1);
	ПутьНаСервере = Сред(АдресБезПротокола, Курсор);
	
	Результат.Вставить("АдресСервера", АдресСервера);
	Результат.Вставить("ПутьНаСервере", ПутьНаСервере);
	
	Возврат Результат;

КонецФункции

#КонецОбласти
