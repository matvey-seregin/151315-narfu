
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Адрес = Константы.АдресЦентральнойБазы.Получить();
	Логин = Константы.ЛогинЦентральнойБазы.Получить();
	Пароль = Константы.ПарольЦентральнойБазы.Получить();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПарольПриИзменении(Элемент)
	СохранитьНастройки();
КонецПроцедуры

&НаКлиенте
Процедура ЛогинПриИзменении(Элемент)
	СохранитьНастройки();
КонецПроцедуры

&НаКлиенте
Процедура АдресПриИзменении(Элемент)
	СохранитьНастройки();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьОбмен(Команда)
	ВыполнитьОбменНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьДанныеЗаказов(Команда)
	ОтправитьДанныеЗаказовНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьНастройки()
	
	Константы.АдресЦентральнойБазы.Установить(Адрес);
	Константы.ЛогинЦентральнойБазы.Установить(Логин);
	Константы.ПарольЦентральнойБазы.Установить(Пароль);
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьОбменНаСервере()
	
	НастройкиПодключения = НастройкиПодключения();
	
	Соединение = Новый HTTPСоединение(НастройкиПодключения.АдресСервера,,
		НастройкиПодключения.Логин,
		НастройкиПодключения.Пароль,,,
		НастройкиПодключения.ЗащищенноеСоединение);
		
	СистемнаяИнформации = Новый СистемнаяИнформация;
	ИдентификаторКлиента = Строка(СистемнаяИнформации.ИдентификаторКлиента);	
		
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("X-MobileID", ИдентификаторКлиента);
	Заголовки.Вставить("X-Received-Message", 0);
	
	АдресРесурса = НастройкиПодключения.ПутьНаСервере + "/hs/mobile/exchange";
	Запрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	
	//@skip-check bsl-legacy-check-dynamic-feature-access - устарел только на клиенте
	Ответ = Соединение.Получить(Запрос);
	
	Если Ответ.КодСостояния <> 200 Тогда
		ВызватьИсключение "Ошибка обмена";
	КонецЕсли;
	
	ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
	ДанныеОтвета = ПрочитатьЗначениеJSON(ТелоОтвета);
	
	Для Каждого ОписаниеОбъекта Из ДанныеОтвета Цикл
		
		Если ОписаниеОбъекта.Тип = "Справочник.ДоговорыКонтрагентов" Тогда
			ЗаписатьДоговор(ОписаниеОбъекта);
		ИначеЕсли ОписаниеОбъекта.Тип = "Справочник.Контрагенты" Тогда
			ЗаписатьКонтрагента(ОписаниеОбъекта);
		ИначеЕсли ОписаниеОбъекта.Тип = "Справочник.Номенклатура" Тогда 
			ЗаписатьНоменклатуру(ОписаниеОбъекта);
		Иначе
			Продолжить;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ОтправитьДанныеЗаказовНаСервере()
    
    НастройкиПодключения = НастройкиПодключения();

	Соединение = Новый HTTPСоединение(НастройкиПодключения.АдресСервера, , НастройкиПодключения.Логин,
		НастройкиПодключения.Пароль, , , НастройкиПодключения.ЗащищенноеСоединение);
    
    ДанныеДляОтправки = ПолучитьДанныеЗаказов();
    
    JSONДанные = ЗаписатьЗначениеJSON(ДанныеДляОтправки);

    Заголовки = Новый Соответствие;
    Заголовки.Вставить("Content-Type", "application/json");
    
    АдресРесурса = НастройкиПодключения.ПутьНаСервере + "/hs/mobile/receive";
    Запрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
    Запрос.УстановитьТелоИзСтроки(JSONДанные);
    
    Попытка
        //@skip-check bsl-legacy-check-dynamic-feature-access
        Ответ = Соединение.ОтправитьДляОбработки(Запрос);
        
        Если Ответ.КодСостояния = 200 Тогда
            Сообщение = Новый СообщениеПользователю;
            Сообщение.Текст = "Данные успешно отправлены на сервер!";
            Сообщение.Сообщить();
        Иначе
            ВызватьИсключение "Ошибка отправки данных. Код: " + Ответ.КодСостояния;
        КонецЕсли;
        
    Исключение
            Сообщение = Новый СообщениеПользователю;
            Сообщение.Текст = "Ошибка отправки данных";
            Сообщение.Сообщить();
    КонецПопытки;
    
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеЗаказов()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаказПокупателя.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗаказПокупателя КАК ЗаказПокупателя";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	МассивДанных = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		ДанныеДокумента = СформироватьМассивДанных(Выборка.Ссылка);
		МассивДанных.Добавить(ДанныеДокумента);
	КонецЦикла;
	
	Возврат МассивДанных;
	
КонецФункции

&НаСервере
Функция СформироватьМассивДанных(СсылкаНаДокумент)
    
    Документ = СсылкаНаДокумент.ПолучитьОбъект();
    
    ДанныеДокумента = Новый Структура;
    
    ДанныеДокумента.Вставить("Тип", "Документ.ЗаказПокупателя");
    ДанныеДокумента.Вставить("Дата", Формат(Документ.Дата, "ДФ=yyyyMMddHHmmss"));
    ДанныеДокумента.Вставить("Номер", Документ.Номер);
    ДанныеДокумента.Вставить("Комментарий", Документ.Комментарий);
    ДанныеДокумента.Вставить("Проведен", Документ.Проведен);
    ДанныеДокумента.Вставить("Ссылка", Строка(Документ.Ссылка.УникальныйИдентификатор()));

    
    Если Не Документ.Контрагент.Пустая() Тогда
        ДанныеДокумента.Вставить("Контрагент", Строка(Документ.Контрагент.УникальныйИдентификатор()));
    Иначе
        ДанныеДокумента.Вставить("Контрагент", "");
    КонецЕсли;
    
    Если Не Документ.Договор.Пустая() Тогда
        ДанныеДокумента.Вставить("Договор", Строка(Документ.Договор.УникальныйИдентификатор()));
    Иначе
        ДанныеДокумента.Вставить("Договор", "");
    КонецЕсли;
    
    Если Документ.СписокНоменклатуры.Количество() > 0 Тогда
        МассивНоменклатуры = Новый Массив;
        
        Для Каждого СтрокаНоменклатуры Из Документ.СписокНоменклатуры Цикл
            ДанныеСтроки = Новый Структура;
            
            Если Не СтрокаНоменклатуры.Номенклатура.Пустая() Тогда
                ДанныеСтроки.Вставить("Номенклатура", Строка(СтрокаНоменклатуры.Номенклатура.УникальныйИдентификатор()));
            Иначе
                ДанныеСтроки.Вставить("Номенклатура", "");
            КонецЕсли;
            
            // Количество
            ДанныеСтроки.Вставить("Количество", СтрокаНоменклатуры.Количество);
            
            МассивНоменклатуры.Добавить(ДанныеСтроки);
        КонецЦикла;
        
        ДанныеДокумента.Вставить("СписокНоменклатуры", МассивНоменклатуры);
    Иначе
        ДанныеДокумента.Вставить("СписокНоменклатуры", Новый Массив);
    КонецЕсли;
    
    Если ЗначениеЗаполнено(Документ.ФотоДоговора) Тогда
        Попытка
            ДвоичныеДанные = Документ.ФотоДоговора.Получить();
            Если ДвоичныеДанные <> Неопределено Тогда
                ДанныеДокумента.Вставить("ФотоДоговораBase64", Base64Строка(ДвоичныеДанные));
            Иначе
                ДанныеДокумента.Вставить("ФотоДоговораBase64", "");
            КонецЕсли;
        Исключение
            СообщениеПользователю = Новый СообщениеПользователю;
            СообщениеПользователю.Текст = "Не удалось получить фото договора: " + ОписаниеОшибки();
            СообщениеПользователю.Сообщить();
            ДанныеДокумента.Вставить("ФотоДоговораBase64", "");
        КонецПопытки;
    Иначе
        ДанныеДокумента.Вставить("ФотоДоговораBase64", "");
    КонецЕсли;

    
    Возврат ДанныеДокумента;
    
КонецФункции

&НаСервере
Процедура ЗаписатьНоменклатуру(ОписаниеОбъекта)
	
	Ссылка = Справочники.Номенклатура.ПолучитьСсылку(
		Новый УникальныйИдентификатор(ОписаниеОбъекта.Идентификатор));
		
	СпрОбъект = Ссылка.ПолучитьОбъект();
	Если СпрОбъект = Неопределено Тогда
		Если ОписаниеОбъекта.ЭтоГруппа Тогда
			СпрОбъект = Справочники.Номенклатура.СоздатьГруппу();
		Иначе
			СпрОбъект = Справочники.Номенклатура.СоздатьЭлемент();
		КонецЕсли;
		СпрОбъект.УстановитьСсылкуНового(Ссылка);
	КонецЕсли;
	
	СпрОбъект.Наименование = ОписаниеОбъекта.Наименование;
	СпрОбъект.Родитель = Справочники.Номенклатура.ПолучитьСсылку(
		Новый УникальныйИдентификатор(ОписаниеОбъекта.Родитель));
		
	СпрОбъект.ОбменДанными.Загрузка = Истина;
	
	СпрОбъект.Записать();

КонецПроцедуры

&НаСервере
Процедура ЗаписатьКонтрагента(ОписаниеОбъекта)
	
	Ссылка = Справочники.Контрагенты.ПолучитьСсылку(
		Новый УникальныйИдентификатор(ОписаниеОбъекта.Идентификатор));
		
	СпрОбъект = Ссылка.ПолучитьОбъект();
	Если СпрОбъект = Неопределено Тогда
		СпрОбъект = Справочники.Контрагенты.СоздатьЭлемент();
		СпрОбъект.УстановитьСсылкуНового(Ссылка);
	КонецЕсли;
	
	СпрОбъект.Наименование = ОписаниеОбъекта.Наименование;
		
	СпрОбъект.ОбменДанными.Загрузка = Истина;
	
	СпрОбъект.Записать();

КонецПроцедуры

&НаСервере
Процедура ЗаписатьДоговор(ОписаниеОбъекта)
	
	Ссылка = Справочники.ДоговорыКонтрагентов.ПолучитьСсылку(
		Новый УникальныйИдентификатор(ОписаниеОбъекта.Идентификатор));
		
	СпрОбъект = Ссылка.ПолучитьОбъект();
	Если СпрОбъект = Неопределено Тогда
		СпрОбъект = Справочники.ДоговорыКонтрагентов.СоздатьЭлемент();
		СпрОбъект.УстановитьСсылкуНового(Ссылка);
	КонецЕсли;
	
	СпрОбъект.Наименование = ОписаниеОбъекта.Наименование;
	СпрОбъект.Владелец = Справочники.Контрагенты.ПолучитьСсылку(
		Новый УникальныйИдентификатор(ОписаниеОбъекта.Владелец));
		
	СпрОбъект.ОбменДанными.Загрузка = Истина;
	
	СпрОбъект.Записать();

КонецПроцедуры

&НаСервере
Функция НастройкиПодключения()
	
	Результат = Новый Структура;
	Результат.Вставить("Логин", Логин);
	Результат.Вставить("Пароль", Пароль);
	
	Если СтрНачинаетсяС(Адрес, "https") Тогда
		ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL();
	Иначе
		ЗащищенноеСоединение = Неопределено;
	КонецЕсли;		
		
	Результат.Вставить("ЗащищенноеСоединение", ЗащищенноеСоединение);
	
	Курсор = СтрНайти(Адрес, "://");
	АдресБезПротокола = Сред(Адрес, Курсор + 3);
	
	Курсор = СтрНайти(АдресБезПротокола, "/");
	АдресСервера = Лев(АдресБезПротокола, Курсор - 1);
	ПутьНаСервере = Сред(АдресБезПротокола, Курсор);
	
	Результат.Вставить("АдресСервера", АдресСервера);
	Результат.Вставить("ПутьНаСервере", ПутьНаСервере);
	
	Возврат Результат;

КонецФункции

#КонецОбласти
