
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Список.Параметры.УстановитьЗначениеПараметра("Организация", Параметры.Организация);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Фильтр = Новый Структура;
	Фильтр.Вставить("Номенклатура", ВыбраннаяСтрока);
	
	НайденныеСтроки = Корзина.НайтиСтроки(Фильтр);
	
	Если ЗначениеЗаполнено(НайденныеСтроки) Тогда
		Строка = НайденныеСтроки[0];
		Строка.Количество = Строка.Количество + 1;
	Иначе
		Строка = Корзина.Добавить();
		Строка.Номенклатура = ВыбраннаяСтрока;
		Строка.Количество = 1;
		Строка.Цена = ТекущиеДанные.Цена;
	КонецЕсли;
	
	РассчитатьСуммуСтроки(Строка);

КонецПроцедуры

&НаКлиенте
Процедура СписокНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные.ЭтоГруппа Тогда
		Выполнение = Ложь;
		Возврат;
	КонецЕсли;
	
	Данные = Новый Структура;
	Данные.Вставить("Номенклатура", ТекущиеДанные.Ссылка);
	Данные.Вставить("Цена", ТекущиеДанные.Цена);
	
	ПараметрыПеретаскивания.Значение = Данные;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКорзина

&НаКлиенте
Процедура КорзинаЦенаПриИзменении(Элемент)
	
	СтрокаТабЧасти = Элементы.СписокНоменклатуры.ТекущиеДанные;
	РассчитатьСуммуСтроки(СтрокаТабЧасти);
		
КонецПроцедуры

&НаКлиенте
Процедура КорзинаКоличествоПриИзменении(Элемент)
	
	СтрокаТабЧасти = Элементы.СписокНоменклатуры.ТекущиеДанные;
	РассчитатьСуммуСтроки(СтрокаТабЧасти);
	
КонецПроцедуры

&НаКлиенте
Процедура КорзинаНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.СписокНоменклатуры.ТекущиеДанные;
	Цена = ЦеныНоменклатурыВызовСервера.ЦенаНоменклатуры(
		ТекущиеДанные.Номенклатура, Параметры.Организация);
			
	ТекущиеДанные.Цена = Цена;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура КорзинаПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура КорзинаПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	Данные = ПараметрыПеретаскивания.Значение;
	
	Фильтр = Новый Структура;
	Фильтр.Вставить("Номенклатура", Данные.Номенклатура);
	
	НайденныеСтроки = Корзина.НайтиСтроки(Фильтр);
	
	Если ЗначениеЗаполнено(НайденныеСтроки) Тогда
		Строка = НайденныеСтроки[0];
		Строка.Количество = Строка.Количество + 1;
	Иначе
		Строка = Корзина.Добавить();
		Строка.Номенклатура = Данные.Номенклатура;
		Строка.Количество = 1;
		Строка.Цена = Данные.Цена;
	КонецЕсли;
	
	РассчитатьСуммуСтроки(Строка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьПодбор(Команда)
	
	Адрес = ПоместитьКорзинуВоВременноеХранилище();
	ОповеститьОВыборе(Адрес);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПоместитьКорзинуВоВременноеХранилище()
	
	Возврат ПоместитьВоВременноеХранилище(Корзина.Выгрузить());
	
КонецФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(Строка)
	Строка.Сумма = Строка.Количество * Строка.Цена;
КонецПроцедуры

#КонецОбласти



