#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОтправитьВМобильноеПриложениеТорговогоПредставителя();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОтправитьВМобильноеПриложениеТорговогоПредставителя()
	
	Если ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоПокупатель() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОбменСМобильнымПриложением.Ссылка КАК Ссылка
		|ИЗ
		|	ПланОбмена.ОбменСМобильнымПриложением КАК ОбменСМобильнымПриложением
		|ГДЕ
		|	НЕ ОбменСМобильнымПриложением.ПометкаУдаления
		|	И НЕ ОбменСМобильнымПриложением.ЭтотУзел";
		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ОбменДанными.Получатели.Добавить(Выборка.Ссылка);
	КонецЦикла;
		
КонецПроцедуры

Функция ЭтоПокупатель()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	ДоговорыКонтрагентов.Владелец = &Владелец
		|	И ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидДоговора.СПокупателем)";

	Запрос.УстановитьПараметр("Владелец", Ссылка);
	
	Возврат Не Запрос.Выполнить().Пустой();

КонецФункции

#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
