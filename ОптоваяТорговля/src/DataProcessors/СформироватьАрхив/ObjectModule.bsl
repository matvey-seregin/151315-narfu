#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Готовит архив, содержащий список номенклатуры и файлы фотографий
// 
// Возвращаемое значение:
//  Строка -  Адрес временного хранилища с архивом
Функция ПодготовитьАрхив()Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕ(УНИКАЛЬНЫЙИДЕНТИФИКАТОР(СпрНоменклатура.Ссылка)) КАК Идентификатор,
		|	ПРЕДСТАВЛЕНИЕ(СпрНоменклатура.Ссылка) КАК Номенклатура,
		|	ПРЕДСТАВЛЕНИЕ(СпрНоменклатура.ТипНоменклатуры) КАК ТипНоменклатуры,
		|	СпрНоменклатура.Фото КАК Фото,
		|	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) КАК Цена
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних КАК ЦеныНоменклатурыСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО ЦеныНоменклатурыСрезПоследних.Номенклатура = СпрНоменклатура.Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ИмяКаталога = ПолучитьИмяВременногоФайла("");
	СоздатьКаталог(ИмяКаталога);
	
	ОписаниеНоменклатуры = Новый Массив;
	
	ШаблонИмениКартинки = "%1%2%3";
	
	Пока Выборка.Следующий() Цикл
		
		Описание = Новый Структура;
		Описание.Вставить("Идентификатор", Выборка.Идентификатор);
		Описание.Вставить("Номенклатура", Выборка.Номенклатура);
		Описание.Вставить("ТипНоменклатуры", Выборка.ТипНоменклатуры);
		Описание.Вставить("Цена", Выборка.Цена);
		
		ДанныеФото = Выборка.Фото.Получить();
		
		Если ДанныеФото = Неопределено Тогда
			Описание.Вставить("Фото", Неопределено);
		Иначе
			ИмяФайла = СтрШаблон(ШаблонИмениКартинки, ИмяКаталога, ПолучитьразделительПути(), 
				Выборка.Идентификатор);
			ДанныеФото.Записать(ИмяФайла);
			Описание.Вставить("Фото", СтрШаблон(ШаблонИмениКартинки, "", ПолучитьразделительПути(),
				 Выборка.Идентификатор));
		КонецЕсли;
		
		ОписаниеНоменклатуры.Добавить(Описание);
		
	КонецЦикла;
	
	ШаблонИмениВыгрузки  = "%1%2export.json";
	
	ИмяФайлаВыгрузки = СтрШаблон(ШаблонИмениВыгрузки, ИмяКаталога, ПолучитьРазделительПути());
	
	Запись = Новый ЗаписьJSON();
	Запись.ОткрытьФайл(ИмяФайлаВыгрузки);
	ЗаписатьJSON(Запись, ОписаниеНоменклатуры);
	Запись.Закрыть();
	
	Архиватор = Новый ЗаписьФайлаАрхива;
	Архиватор.Добавить(ИмяКаталога + ПолучитьРазделительПути() + "*.*",,
		 РежимОбработкиПодкаталоговФайлаАрхива.ОбрабатыватьРекурсивно);
	ДанныеАрхива = Архиватор.ПолучитьДвоичныеДанные();
	
	УдалитьФайлы(ИмяКаталога);
	
	Возврат ПоместитьВоВременноеХранилище(ДанныеАрхива);
	
КонецФункции
#КонецОбласти

#КонецЕсли