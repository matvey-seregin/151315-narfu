
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	РаботаСДоговорамиКонтрагентов.ПроверитьДоговорВДокументе(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	СформироватьДвиженияОстаткиТоваров();
	СформироватьДвиженияЗадолженностьПередПоставщиками();

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СуммаДокумента = СписокНоменклатуры.Итог("Сумма");
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьДвиженияОстаткиТоваров()
	
	// регистр ОстаткиТоваров
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПокупкаСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	ПокупкаСписокНоменклатуры.Количество КАК Количество,
		|	ПокупкаСписокНоменклатуры.Сумма КАК Сумма
		|ИЗ
		|	Документ.Покупка.СписокНоменклатуры КАК ПокупкаСписокНоменклатуры
		|ГДЕ
		|	ПокупкаСписокНоменклатуры.Ссылка = &Ссылка
		|	И ПокупкаСписокНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипНоменклатуры.Товар)";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Движения.ОстаткиТоваров.Записывать = Истина;
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ОстаткиТоваров.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Количество = Выборка.Количество;
		Движение.Сумма = Выборка.Сумма;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СформироватьДвиженияЗадолженностьПередПоставщиками()

	Движения.АвансыПоставщикам.Записывать = Истина;
	Движения.Записать();
	
	Движения.ЗадолженостьПередПоставщиками.Записывать = Истина;
	Движения.АвансыПоставщикам.Записывать = Истина;
	
	Блокировка = Новый БлокировкаДанных;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ЗадолженостьПередПоставщиками");
	ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
	ЭлементБлокировки.УстановитьЗначение("Договор", ДоговорКонтрагента);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.АвансыПоставщикам");
	ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
	ЭлементБлокировки.УстановитьЗначение("Договор", ДоговорКонтрагента);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	//@skip-check lock-out-of-try - блокировка регистров при проведении документа
	Блокировка.Заблокировать();	

	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АвансыПоставщикамОстатки.СуммаОстаток КАК Аванс
		|ИЗ
		|	РегистрНакопления.АвансыПоставщикам.Остатки(
		|			&Дата,
		|			Контрагент = &Контрагент
		|				И Договор = &Договор) КАК АвансыПоставщикамОстатки";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Договор", ДоговорКонтрагента);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Аванс = 0;
	
	Если РезультатЗапроса.Следующий() Тогда
		Аванс = РезультатЗапроса.Аванс;
	КонецЕсли;
	
	Если Аванс > 0 Тогда
		
		СуммаАванса = Мин(Аванс, СуммаДокумента);
		
		ДвижениеАванс = Движения.АвансыПоставщикам.Добавить();
		ДвижениеАванс.Период = Дата;
		ДвижениеАванс.ВидДвижения = ВидДвиженияНакопления.Расход;
		ДвижениеАванс.Контрагент = Контрагент;
		ДвижениеАванс.Договор = ДоговорКонтрагента;
		ДвижениеАванс.Сумма = СуммаАванса;
		
		Если СуммаДокумента > Аванс Тогда
			Движение = Движения.ЗадолженостьПередПоставщиками.Добавить();
			Движение.Период = Дата;
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Контрагент = Контрагент;
			Движение.Договор = ДоговорКонтрагента;
			Движение.Сумма = СуммаДокумента - Аванс;
		КонецЕсли;
		
	Иначе
		Движение = Движения.ЗадолженостьПередПоставщиками.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Контрагент = Контрагент;
		Движение.Договор = ДоговорКонтрагента;
		Движение.Сумма = СуммаДокумента - Аванс;
		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли