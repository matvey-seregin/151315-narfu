
#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокНоменклатуры

&НаКлиенте
Процедура СписокНоменклатурыЦенаПриИзменении(Элемент)
	
	СтрокаТабЧасти = Элементы.СписокНоменклатуры.ТекущиеДанные;
	РассчитатьСуммуСтроки(СтрокаТабЧасти);
		
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыКоличествоПриИзменении(Элемент)
	
	СтрокаТабЧасти = Элементы.СписокНоменклатуры.ТекущиеДанные;
	РассчитатьСуммуСтроки(СтрокаТабЧасти);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.СписокНоменклатуры.ТекущиеДанные;
	Цена = ЦеныНоменклатурыВызовСервера.ЦенаНоменклатуры(
		ТекущиеДанные.Номенклатура, Объект.Организация, Объект.Дата);
			
	ТекущиеДанные.Цена = Цена;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНоменклатурыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ЭтоАдресВременногоХранилища(ВыбранноеЗначение) Тогда
		ЗаполнитьИзКорзины(ВыбранноеЗначение);
		Модифицированность = Истина;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтчетПоОстаткам(Команда)
	
	ОтборНоменклатура = Новый Массив;
	Для Каждого Строка Из Объект.СписокНоменклатуры Цикл
		ОтборНоменклатура.Добавить(Строка.Номенклатура);
	КонецЦикла;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Номенклатура", ОтборНоменклатура);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", Отбор);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	ОткрытьФорму("Отчет.ОстаткиТоваров.Форма", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура Подбор(Команда)
	
	Если ЗначениеЗаполнено(Объект.СписокНоменклатуры) Тогда
		Ответ = Ждать ВопросАсинх(
			"После завершения подбора текущие данные таблицы будут заменены. Продолжить?",
			РежимДиалогаВопрос.ДаНет);
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	
	ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаПодбораСКорзиной", ПараметрыФормы, Элементы.СписокНоменклатуры);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьИзКорзины(Адрес)
	
	Корзина = ПолучитьИзВременногоХранилища(Адрес);
	Объект.СписокНоменклатуры.Загрузить(Корзина);
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуСтроки(Строка)
	Строка.Сумма = Строка.Количество * Строка.Цена;
КонецПроцедуры

#КонецОбласти



