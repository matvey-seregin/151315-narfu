#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура ЗагрузитьНоменклатуру(Команда)
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.Заголовок = "Выбере файл для загрузки";
	ПараметрыДиалога.Фильтр = "Таблица EXCEL | *.xls; *.xlsx";
	
	ОписаниеФайла = Ждать ПоместитьФайлНаСерверАсинх(,,,ПараметрыДиалога);
	
	Если ОписаниеФайла <> Неопределено Тогда
		ЗаполнениеТаблицыЦенНоменклатуры(ОписаниеФайла.Адрес, ОписаниеФайла.СсылкаНаФайл.Расширение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьНоменклатуру(Команда)
	
	Если Объект.ЦеныНоменклатуры.Количество() = 0 Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Таблица номенклатуры пуста";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогВыбораФайла.Заголовок = "Выберите место для сохранения файла";
	ДиалогВыбораФайла.ПолноеИмяФайла = "Номенклатура.xlsx";
	ДиалогВыбораФайла.Фильтр = "Таблица EXCEL | *.xls; *.xlsx";
	
	ПоказатьДиалогВыбораФайлаАсинх(ДиалогВыбораФайла);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнениеТаблицыЦенНоменклатуры(Адрес, Расширение)
	
	ПутьКФайлу = ПолучитьИмяВременногоФайла(Расширение);
	
	Данные = ПолучитьИзВременногоХранилища(Адрес);
	Данные.Записать(ПутьКФайлу);
	
	ТабДок = Новый ТабличныйДокумент();
	
	Попытка
		ТабДок.Прочитать(ПутьКФайлу);
	Исключение
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = СтрШаблон("Не удалось прочитать файл по причине: %1", ОписаниеОшибки());
		Сообщение.Сообщить();
	КонецПопытки;
	
	Построитель = Новый ПостроительЗапроса;
	Построитель.ИсточникДанных = Новый ОписаниеИсточникаДанных(ТабДок.Область());
	Построитель.ЗаполнитьНастройки();
	
	Построитель.Выполнить();
	
	ТаблицаЗначенийИзExcel = Построитель.Результат.Выгрузить();
	
	СоответствиеНоменклатур = ЗагрузитьСоответствиеНоменклатур(ТаблицаЗначенийИзExcel);
	
	Для Каждого Элемент из ТаблицаЗначенийИзExcel Цикл
		
		НоменклатураОбъект = СоответствиеНоменклатур.Получить(Элемент.Номенклатура);
		
		Если НоменклатураОбъект = Неопределено Тогда 
			НоменклатураОбъект = СоздатьНоменклатуру(Элемент);
		КонецЕсли;
		
		Если НоменклатураОбъект = Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Объект.ЦеныНоменклатуры.Добавить();
		НоваяСтрока.Номенклатура = НоменклатураОбъект;
		НоваяСтрока.Цена = Элемент.Цена;
		
	КонецЦикла;
	
	УдалитьФайлы(ПутьКФайлу);
	
КонецПроцедуры

&НаСервере
Функция ЗагрузитьСоответствиеНоменклатур(ТаблицаДанных)
	
	УникальныеНаименования = Новый Массив;
	
	Для Каждого Строка Из ТаблицаДанных Цикл
		Если ЗначениеЗаполнено(Строка.Номенклатура) Тогда
			УникальныеНаименования.Добавить(Строка.Номенклатура);
		КонецЕсли;
	КонецЦикла;
	
	Если УникальныеНаименования.Количество() = 0 Тогда
		Возврат Новый Соответствие;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Ссылка,
		|	Номенклатура.Наименование КАК Наименование
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Наименование В(&Наименования)";
	
	Запрос.УстановитьПараметр("Наименования", УникальныеНаименования);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Соответствие = Новый Соответствие;
	
	Пока Выборка.Следующий() Цикл
		Соответствие.Вставить(Выборка.Наименование, Выборка.Ссылка);
	КонецЦикла;
	
	Возврат Соответствие;
	
КонецФункции

&НаСервере
Функция СоздатьНоменклатуру(ДанныеНоменклатуры)
	
	Если НЕ ЗначениеЗаполнено(ДанныеНоменклатуры.Номенклатура) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ДанныеНоменклатуры.ВидНоменклатуры = "Товар" Тогда
		ВидНоменклатуры = Перечисления.ТипНоменклатуры.Товар;
	Иначе
		ВидНоменклатуры = Перечисления.ТипНоменклатуры.Услуга;	
	КонецЕсли;
	
	Попытка
		НоваяНоменклатура = Справочники.Номенклатура.СоздатьЭлемент();
		НоваяНоменклатура.Наименование = ДанныеНоменклатуры.Номенклатура;
		НоваяНоменклатура.ТипНоменклатуры = ВидНоменклатуры;
		НоваяНоменклатура.Записать();
		
		Возврат НоваяНоменклатура.Ссылка;
	Исключение
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Не удалось создать номенклатуру: " + ДанныеНоменклатуры.Номенклатура;
		Сообщение.Сообщить();
		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьДиалогВыбораФайлаАсинх(ДиалогВыбораФайла)
	
	ДиалогВыбораФайла.Показать(Новый ОписаниеОповещения("ПослеВыбораФайла", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораФайла(РезультатыВыбора, ДополнительныеПараметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(РезультатыВыбора) Тогда
		Возврат;
	КонецЕсли;
	
	ПутьФайла = РезультатыВыбора[0];
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	ЗаполнитьТабличныйДокументНаСервере(ТабличныйДокумент);
	ЗаписьТабличногоДокумента(ТабличныйДокумент, ПутьФайла)

КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗаписьТабличногоДокумента(ТабличныйДокумент, ПутьФайла)
	Попытка
		ТабличныйДокумент.ЗаписатьАсинх(ПутьФайла, ТипФайлаТабличногоДокумента.XLSX);
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Файл записан";
		Сообщение.Сообщить();
	Исключение
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Не удалось записать Файл";
		Сообщение.Сообщить();
	КонецПопытки;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТабличныйДокументНаСервере(ТабличныйДокумент)
	
	МассивСсылок = Новый Массив;
	Для Каждого СтрокаТаблицы Из Объект.ЦеныНоменклатуры Цикл
		Если ЗначениеЗаполнено(СтрокаТаблицы.Номенклатура) Тогда
			МассивСсылок.Добавить(СтрокаТаблицы.Номенклатура);
		КонецЕсли;
	КонецЦикла;
	
	СоответствиеТипов = ЗагрузитьТипыНоменклатур(МассивСсылок);
	
	Макет = Документы.УстановкаЦенНоменклатуры.ПолучитьМакет("МакетДляВыгрузкиExcel");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("СтрокаТаблицы");
	
	ТабличныйДокумент.Вывести(ОбластьШапка);
	
	Для Каждого СтрокаТаблицы Из Объект.ЦеныНоменклатуры Цикл
		ОбластьСтрока.Параметры.Заполнить(СтрокаТаблицы);
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.Номенклатура) Тогда
			ТипНоменклатуры = СоответствиеТипов.Получить(СтрокаТаблицы.Номенклатура);
			ОбластьСтрока.Параметры.ВидНоменклатуры = ТипНоменклатуры;
		Иначе
			ОбластьСтрока.Параметры.ВидНоменклатуры = Неопределено;
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьСтрока);	
	КонецЦикла;

КонецПроцедуры

&НаСервере
Функция ЗагрузитьТипыНоменклатур(МассивСсылок)
	
	Если МассивСсылок.Количество() = 0 Тогда
		Возврат Новый Соответствие;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Ссылка,
		|	Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Ссылка В(&Ссылки)";
	
	Запрос.УстановитьПараметр("Ссылки", МассивСсылок);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Соответствие = Новый Соответствие;
	
	Пока Выборка.Следующий() Цикл
		Соответствие.Вставить(Выборка.Ссылка, Выборка.ТипНоменклатуры);
	КонецЦикла;
	
	Возврат Соответствие;
	
КонецФункции

#КонецОбласти