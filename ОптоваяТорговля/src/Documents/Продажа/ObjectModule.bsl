#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	РаботаСДоговорамиКонтрагентов.ПроверитьДоговорВДокументе(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ПодготовитьСписокНоменклатурыДляПроведения(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	СформироватьДвиженияОстаткиТоваров(Отказ);
	
	СформироватьДвиженияПродажи(Отказ);
	
	СформироватьДвиженияЗадолженностьПокупателей(Отказ);
	
КонецПроцедуры

//@skip-check reading-attribute-from-database - сформировано конструктором
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		// Заполнение шапки
		Организация = ДанныеЗаполнения.Организация;
		Контрагент = ДанныеЗаполнения.Контрагент;
		ДоговорКонтрагента = ДанныеЗаполнения.ДоговорКонтрагента;
		СкладОтгрузки = ДанныеЗаполнения.СкладОтгрузки;
		СуммаДокумента = ДанныеЗаполнения.СуммаДокумента;

		Для Каждого ТекСтрокаСписокНоменклатуры из ДанныеЗаполнения.СписокНоменклатуры Цикл
			НоваяСтрока = СписокНоменклатуры.Добавить();
			НоваяСтрока.Номенклатура = ТекСтрокаСписокНоменклатуры.Номенклатура;
			НоваяСтрока.Цена = ТекСтрокаСписокНоменклатуры.Цена;
			НоваяСтрока.Количество = ТекСтрокаСписокНоменклатуры.Количество;
			НоваяСтрока.Сумма = ТекСтрокаСписокНоменклатуры.Сумма;
		КонецЦикла;
		
	КонецЕсли;
	
	Ответственный = ПараметрыСеанса.ТекущийПользователь;

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СуммаДокумента = СписокНоменклатуры.Итог("Сумма");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьСписокНоменклатурыДляПроведения(Отказ)
	
	Движения.ОстаткиТоваров.Записывать = Истина;
	Движения.Записать();	
	
	Блокировка = Новый БлокировкаДанных;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиТоваров");
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	//@skip-check lock-out-of-try - блокировка регистров при проведении документа
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПродажаСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(ПродажаСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(ПродажаСписокНоменклатуры.Сумма) КАК Сумма,
		|	МИНИМУМ(ПродажаСписокНоменклатуры.НомерСтроки) КАК НомерСтроки
		|ПОМЕСТИТЬ ВТ_ДанныеДокумента
		|ИЗ
		|	Документ.Продажа.СписокНоменклатуры КАК ПродажаСписокНоменклатуры
		|ГДЕ
		|	ПродажаСписокНоменклатуры.Ссылка = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	ПродажаСписокНоменклатуры.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДанныеДокумента.Номенклатура КАК Номенклатура,
		|	ВТ_ДанныеДокумента.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
		|	ВТ_ДанныеДокумента.Количество КАК Количество,
		|	ВТ_ДанныеДокумента.Сумма КАК Сумма,
		|	ВТ_ДанныеДокумента.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ОстаткиТоваровОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ОстаткиТоваровОстатки.СуммаОстаток, 0) КАК СуммаОстаток
		|ИЗ
		|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваров.Остатки(&Период, Номенклатура В
		|			(ВЫБРАТЬ
		|				ВТ_ДанныеДокумента.Номенклатура
		|			ИЗ
		|				ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента)) КАК ОстаткиТоваровОстатки
		|		ПО ВТ_ДанныеДокумента.Номенклатура = ОстаткиТоваровОстатки.Номенклатура";
		
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", МоментВремени());
	
	СписокНоменклатурыДляПроведения = НовыйСписокНоменклатурыДляПроведения();
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ТипНоменклатуры = Перечисления.ТипНоменклатуры.Товар Тогда
			
			Если Выборка.Количество > Выборка.КоличествоОстаток Тогда
				Отказ = Истина;
				ШаблонСообщения = "Не хватает товара %1. В наличии %2, требуется %3";
				ШаблонПоля = "СписокНоменклатуры[%1].Количество";
				Сообщение = Новый СообщениеПользователю;
				Сообщение.УстановитьДанные(ЭтотОбъект);
				Сообщение.Поле = СтрШаблон(ШаблонПоля, XMLСтрока(Выборка.НомерСтроки - 1));
				Сообщение.Текст = 
					СтрШаблон(ШаблонСообщения, Выборка.Номенклатура, Выборка.КоличествоОстаток, Выборка.Количество);
				Сообщение.Сообщить();
				Продолжить;
			КонецЕсли;
			
			Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
				Себестоимость = Выборка.СуммаОстаток;
			Иначе
				Себестоимость = Выборка.Количество * Выборка.СуммаОстаток / Выборка.КоличествоОстаток;
			КонецЕсли;
			
			СтрокаТаблицы = СписокНоменклатурыДляПроведения.Добавить();
			СтрокаТаблицы.Номенклатура = Выборка.Номенклатура;
			СтрокаТаблицы.ТипНоменклатуры = Выборка.ТипНоменклатуры;
			СтрокаТаблицы.Количество = Выборка.Количество;
			СтрокаТаблицы.Сумма = Выборка.Сумма;
			СтрокаТаблицы.Себестоимость = Себестоимость;
			
		Иначе
			
			СтрокаТаблицы = СписокНоменклатурыДляПроведения.Добавить();
			СтрокаТаблицы.Номенклатура = Выборка.Номенклатура;
			СтрокаТаблицы.ТипНоменклатуры = Выборка.ТипНоменклатуры;
			СтрокаТаблицы.Количество = Выборка.Количество;
			СтрокаТаблицы.Сумма = Выборка.Сумма;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ДополнительныеСвойства.Вставить("СписокТоваровДляПроведения", СписокНоменклатурыДляПроведения);
	
КонецПроцедуры

Процедура СформироватьДвиженияОстаткиТоваров(Отказ)
	
	Движения.ОстаткиТоваров.Записывать = Истина;
	Для Каждого Строка Из ДополнительныеСвойства.СписокТоваровДляПроведения Цикл
		
		Если Строка.ТипНоменклатуры <> Перечисления.ТипНоменклатуры.Товар Тогда
			Продолжить;
		КонецЕсли;
		
		Движение = Движения.ОстаткиТоваров.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Номенклатура = Строка.Номенклатура;
		Движение.Количество = Строка.Количество;
		Движение.Сумма = Строка.Себестоимость;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СформироватьДвиженияПродажи(Отказ)

	Движения.Продажи.Записывать = Истина;
	Для Каждого Строка Из ДополнительныеСвойства.СписокТоваровДляПроведения Цикл
		Движение = Движения.Продажи.Добавить();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Номенклатура = Строка.Номенклатура;
		Движение.Количество = Строка.Количество;
		Движение.Сумма = Строка.Сумма;
		Движение.Себестоимость = Строка.Себестоимость;	
	КонецЦикла;
	
КонецПроцедуры

Процедура СформироватьДвиженияЗадолженностьПокупателей(Отказ)

	Движения.АвансыПокупателей.Записывать = Истина;
	Движения.Записать();
	
	Блокировка = Новый БлокировкаДанных;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ЗадолженностьПокупателей");
	ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
	ЭлементБлокировки.УстановитьЗначение("Договор", ДоговорКонтрагента);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.АвансыПокупателей");
	ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
	ЭлементБлокировки.УстановитьЗначение("Договор", ДоговорКонтрагента);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	//@skip-check lock-out-of-try - блокировка регистров при проведении документа
	Блокировка.Заблокировать();	

	Движения.ЗадолженностьПокупателей.Записывать = Истина;
	Движения.АвансыПокупателей.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АвансыПокупателейОстатки.СуммаОстаток КАК Аванс
		|ИЗ
		|	РегистрНакопления.АвансыПокупателей.Остатки(
		|			&Дата,
		|			Контрагент = &Контрагент
		|				И Договор = &Договор) КАК АвансыПокупателейОстатки";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Договор", ДоговорКонтрагента);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Аванс = 0;
	
	Если РезультатЗапроса.Следующий() Тогда
		Аванс = РезультатЗапроса.Аванс;
	КонецЕсли;
	
	Если Аванс > 0 Тогда
		
		СуммаАванса = Мин(Аванс, СуммаДокумента);
		
		ДвижениеАванс = Движения.АвансыПокупателей.Добавить();
		ДвижениеАванс.Период = Дата;
		ДвижениеАванс.ВидДвижения = ВидДвиженияНакопления.Расход;
		ДвижениеАванс.Контрагент = Контрагент;
		ДвижениеАванс.Договор = ДоговорКонтрагента;
		ДвижениеАванс.Сумма = СуммаАванса;
		
		Если СуммаДокумента > Аванс Тогда
			Движение = Движения.ЗадолженностьПокупателей.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = Дата;
			Движение.Контрагент = Контрагент;
			Движение.Договор = ДоговорКонтрагента;
			Движение.Сумма = СуммаДокумента- Аванс;
		КонецЕсли;
		
	Иначе
		
		Движение = Движения.ЗадолженностьПокупателей.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Договор = ДоговорКонтрагента;
		Движение.Сумма = СуммаДокумента;
		
	КонецЕсли;

КонецПроцедуры

Функция НовыйСписокНоменклатурыДляПроведения()
	
	ТипДенежнаяСумма = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Любой));
	ТипКоличество = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0, ДопустимыйЗнак.Неотрицательный));

	СписокТоваровДляПроведения = Новый ТаблицаЗначений;
	СписокТоваровДляПроведения.Колонки.Добавить("Номенклатура", 
		Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	СписокТоваровДляПроведения.Колонки.Добавить("ТипНоменклатуры", 
		Новый ОписаниеТипов("ПеречислениеСсылка.ТипНоменклатуры"));
	СписокТоваровДляПроведения.Колонки.Добавить("Сумма", ТипДенежнаяСумма);
	СписокТоваровДляПроведения.Колонки.Добавить("Количество", ТипКоличество);
	СписокТоваровДляПроведения.Колонки.Добавить("Себестоимость", ТипДенежнаяСумма);
	
	Возврат СписокТоваровДляПроведения;
	
КонецФункции

#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли