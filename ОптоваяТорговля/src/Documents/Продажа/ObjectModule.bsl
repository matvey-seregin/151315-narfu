
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	РаботаСДоговорамиКонтрагентов.ПроверитьДоговорВДокументе(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	СформироватьДвиженияОстаткиТоваров(Отказ);
	
	СформироватьДвиженияПродажи(Отказ);
	
	ВыполнитьКонтрольОстатков(Отказ);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СуммаДокумента = СписокНоменклатуры.Итог("Сумма");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьДвиженияОстаткиТоваров(Отказ)
	
	// регистр ОстаткиТоваров
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПродажаСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	ПродажаСписокНоменклатуры.Количество КАК Количество,
		|	ПродажаСписокНоменклатуры.Сумма КАК Сумма
		|ИЗ
		|	Документ.Продажа.СписокНоменклатуры КАК ПродажаСписокНоменклатуры
		|ГДЕ
		|	ПродажаСписокНоменклатуры.Ссылка = &Ссылка
		|	И ПродажаСписокНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипНоменклатуры.Товар)";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Движения.ОстаткиТоваров.Записывать = Истина;
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ОстаткиТоваров.Добавить();
		Движение.Период = Дата;
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Номенклатура = Выборка.Номенклатура;
		Движение.Количество = Выборка.Количество;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СформироватьДвиженияПродажи(Отказ)

	// регистр Продажи
	Движения.Продажи.Записывать = Истина;
	Для Каждого ТекСтрокаСписокНоменклатуры из СписокНоменклатуры Цикл
		Движение = Движения.Продажи.Добавить();
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Номенклатура = ТекСтрокаСписокНоменклатуры.Номенклатура;
		Движение.Количество = ТекСтрокаСписокНоменклатуры.Количество;
		Движение.Сумма = ТекСтрокаСписокНоменклатуры.Сумма;
	КонецЦикла;
	
КонецПроцедуры

Процедура ВыполнитьКонтрольОстатков(Отказ)
	
	Движения.ОстаткиТоваров.БлокироватьДляИзменения = Истина;
	Движения.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПродажаСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	МИНИМУМ(ПродажаСписокНоменклатуры.НомерСтроки) КАК НомерСтроки
		|ПОМЕСТИТЬ ВТ_ДанныеДокумента
		|ИЗ
		|	Документ.Продажа.СписокНоменклатуры КАК ПродажаСписокНоменклатуры
		|ГДЕ
		|	ПродажаСписокНоменклатуры.Ссылка = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	ПродажаСписокНоменклатуры.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиТоваровОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиТоваровОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ВТ_ДанныеДокумента.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваров.Остатки(&Период, Номенклатура В
		|		(ВЫБРАТЬ
		|			ВТ_ДанныеДокумента.Номенклатура КАК Номенклатура
		|		ИЗ
		|			ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента)) КАК ОстаткиТоваровОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
		|		ПО ОстаткиТоваровОстатки.Номенклатура = ВТ_ДанныеДокумента.Номенклатура
		|ГДЕ
		|	ОстаткиТоваровОстатки.КоличествоОстаток < 0";
		
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Не хватает товара %1 в количестве %2", 
			Выборка.Номенклатура, -Выборка.КоличествоОстаток);
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Поле = СтрШаблон("СписокНоменклатуры[%1].Количество", XMLСтрока(Выборка.НомерСтроки - 1));
		Сообщение.Сообщить();
		Отказ = Истина;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли