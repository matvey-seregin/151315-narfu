#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ,Режим)

	СформироватьДвиженияВзаиморасчетыСПокупателями();
	
КонецПроцедуры

//@skip-check reading-attribute-from-database - сформировано конструктором
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		// Заполнение шапки
		Контрагент = ДанныеЗаполнения.Контрагент;
		ДоговорКонтрагента = ДанныеЗаполнения.ДоговорКонтрагента;
		Сумма = ДанныеЗаполнения.СуммаДокумента;
		Ответственный = ДанныеЗаполнения.Ответственный;
		Комментарий = ДанныеЗаполнения.Комментарий;

	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Продажа") Тогда
		// Заполнение шапки
		Контрагент = ДанныеЗаполнения.Контрагент;
		ДоговорКонтрагента = ДанныеЗаполнения.ДоговорКонтрагента;
		Сумма = ДанныеЗаполнения.СуммаДокумента;
		Ответственный = ДанныеЗаполнения.Ответственный;
		Комментарий = ДанныеЗаполнения.Комментарий;

	КонецЕсли;
	
	Ответственный = ПараметрыСеанса.ТекущийПользователь;

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьДвиженияВзаиморасчетыСПокупателями()
	
	Движения.ЗадолженностьПокупателей.Записывать = Истина;
	Движения.Записать();
	
	Блокировка = Новый БлокировкаДанных;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ЗадолженностьПокупателей");
	ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
	ЭлементБлокировки.УстановитьЗначение("Договор", ДоговорКонтрагента);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.АвансыПокупателей");
	ЭлементБлокировки.УстановитьЗначение("Контрагент", Контрагент);
	ЭлементБлокировки.УстановитьЗначение("Договор", ДоговорКонтрагента);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	//@skip-check lock-out-of-try - блокировка регистров при проведении документа
	Блокировка.Заблокировать();
	
	Движения.ЗадолженностьПокупателей.Записывать = Истина;
	Движения.АвансыПокупателей.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗадолженностьПокупателейОстатки.СуммаОстаток КАК Долг
		|ИЗ
		|	РегистрНакопления.ЗадолженностьПокупателей.Остатки(
		|			&Период,
		|			Контрагент = &Контрагент
		|				И Договор = &Договор) КАК ЗадолженностьПокупателейОстатки";
	
	Запрос.УстановитьПараметр("Договор", ДоговорКонтрагента);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Период", МоментВремени());
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Долг = 0;
	
	Если РезультатЗапроса.Следующий() Тогда
		Долг = РезультатЗапроса.Долг;
	КонецЕсли;
	
	Если Сумма <= Долг Тогда
		
		Движение = Движения.ЗадолженностьПокупателей.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Договор = ДоговорКонтрагента;
		Движение.Контрагент = Контрагент;
		Движение.Сумма = Сумма;
	Иначе
		
		Если Долг > 0 Тогда
			
			Движение = Движения.ЗадолженностьПокупателей.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.Договор = ДоговорКонтрагента;
			Движение.Контрагент = Контрагент;
			Движение.Сумма = Долг;
			
		КонецЕсли;
		
		ОстатокАванса = Сумма - Долг;
		
		Если ОстатокАванса > 0 Тогда
			
			Движение = Движения.АвансыПокупателей.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = Дата;
			Движение.Договор = ДоговорКонтрагента;
			Движение.Контрагент = Контрагент;
			Движение.Сумма = ОстатокАванса;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
	
#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли