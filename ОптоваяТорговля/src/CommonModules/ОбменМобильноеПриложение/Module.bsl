
#Область СлужебныйПрограммныйИнтерфейс

Функция СформироватьДанныеОтвета(ИдентификаторКлиента, НомерПринятогоСообщения) Экспорт
	
	Попытка
		Возврат ПодготовитьОтвет(ИдентификаторКлиента, НомерПринятогоСообщения);	
	Исключение
		ЗаписьЖурналаРегистрации("ОбменСМобильным.Ошибка", УровеньЖурналаРегистрации.Ошибка,,,
		ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
	КонецПопытки;
	
КонецФункции

Функция ОбработатьПолученныеДокументы(Запрос) Экспорт
	
	ТелоЗапроса = Запрос.ПолучитьТелоКакСтроку();
	
	Если ПустаяСтрока(ТелоЗапроса) Тогда
		ВызватьИсключение "Пустое тело запроса";
	КонецЕсли;
	
	МассивДанных = ПрочитатьЗначениеJSON(ТелоЗапроса);
	
	Для Каждого ДанныеОтвета Из МассивДанных Цикл
		
		СсылкаНаДокумент = Документы.ЗаказПокупателя.ПолучитьСсылку(
		Новый УникальныйИдентификатор(ДанныеОтвета.Ссылка));
		
		Если СсылкаНаДокумент.ПолучитьОбъект() = Неопределено Тогда
			ДокументЗаказа = Документы.ЗаказПокупателя.СоздатьДокумент();
		Иначе
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДанныеОтвета.Дата) Тогда
			ДокументЗаказа.Дата = Дата(ДанныеОтвета.Дата);
		Иначе
			ДокументЗаказа.Дата = ТекущаяДатаСеанса();
		КонецЕсли;
		
		ДокументЗаказа.Номер = ДанныеОтвета.Номер;
		
		ДокументЗаказа.Комментарий = ДанныеОтвета.Комментарий;
		Если ЗначениеЗаполнено(ДанныеОтвета.Контрагент) Тогда
			СсылкаНаКонтрагента = Справочники.Контрагенты.ПолучитьСсылку(
			Новый УникальныйИдентификатор(ДанныеОтвета.Контрагент));
			ДокументЗаказа.Контрагент = СсылкаНаКонтрагента;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДанныеОтвета.Договор) Тогда
			СсылкаНаДоговор = Справочники.ДоговорыКонтрагентов.ПолучитьСсылку(
			Новый УникальныйИдентификатор(ДанныеОтвета.Договор));
			ДокументЗаказа.ДоговорКонтрагента = СсылкаНаДоговор;
		КонецЕсли;
		
		Для Каждого ЭлементНоменклатуры Из ДанныеОтвета.СписокНоменклатуры Цикл
			
			СсылкаНаНоменклатуру = Неопределено;
			КоличествоНоменклатуры = 0;
			
			Если ТипЗнч(ЭлементНоменклатуры.Номенклатура) = Тип("Строка") Тогда
				СсылкаНаНоменклатуру = Справочники.Номенклатура.ПолучитьСсылку(
				Новый УникальныйИдентификатор(ЭлементНоменклатуры.Номенклатура));
			КонецЕсли;
			
			КоличествоНоменклатуры = Число(ЭлементНоменклатуры.Количество);
			
			Если ЗначениеЗаполнено(СсылкаНаНоменклатуру) И КоличествоНоменклатуры > 0 Тогда
				НоваяСтрока = ДокументЗаказа.СписокНоменклатуры.Добавить();
				НоваяСтрока.Номенклатура = СсылкаНаНоменклатуру;
				НоваяСтрока.Количество = КоличествоНоменклатуры;
			КонецЕсли;
			
		КонецЦикла;
		Если ЗначениеЗаполнено(ДанныеОтвета.ФотоДоговораBase64) Тогда
			ДвоичныеДанныеФото = Base64Значение(ДанныеОтвета.ФотоДоговораBase64);
			ДокументЗаказа.Фото = Новый ХранилищеЗначения(ДвоичныеДанныеФото);
		КонецЕсли;
		
		Попытка
			
			Если ДанныеОтвета.Проведен = Истина Тогда
				ДокументЗаказа.Записать(РежимЗаписиДокумента.Проведение);
			Иначе
				ДокументЗаказа.Записать(РежимЗаписиДокумента.Запись);
			КонецЕсли;
			
		Исключение
			ОтветДанные = Новый Структура;
			ОтветДанные.Вставить("result", "error");
			ОтветДанные.Вставить("message", "Ошибка при создании документа: " + ОписаниеОшибки());
			
			JSONТекст = ЗаписатьЗначениеJSON(ОтветДанные);
			
			Ответ = Новый HTTPСервисОтвет(500);
			Ответ.УстановитьТелоИзСтроки(JSONТекст);
			Ответ.Заголовки.Вставить("Content-Type", "application/json");
			
			Возврат Ответ;
		КонецПопытки;
		
	КонецЦикла;
	
	ОтветДанные = Новый Структура;
	ОтветДанные.Вставить("result", "success");
	ОтветДанные.Вставить("message", "Документ успешно создан");
	
	JSONТекст = ЗаписатьЗначениеJSON(ОтветДанные);
	
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.УстановитьТелоИзСтроки(JSONТекст);
	Ответ.Заголовки.Вставить("Content-Type", "application/json");
	
	Возврат Ответ;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПодготовитьОтвет(ИдентификаторКлиента, НомерПринятогоСообщения)
	
	Узел = ПланыОбмена.ОбменСМобильнымПриложением.НайтиПоКоду(ИдентификаторКлиента);
	
	Если Не ЗначениеЗаполнено(Узел) Тогда
		УзелОбъект = ПланыОбмена.ОбменСМобильнымПриложением.СоздатьУзел();
		УзелОбъект.Код = ИдентификаторКлиента;
		УзелОбъект.Пользователь = ПараметрыСеанса.ТекущийПользователь;
		УзелОбъект.Наименование = УзелОбъект.Пользователь;
		УзелОбъект.Записать();
		ПервичнаяРегистрация(УзелОбъект.Ссылка);
		Узел = УзелОбъект.Ссылка;
	КонецЕсли;
	
	ВыборкаИзменений = ПланыОбмена.ВыбратьИзменения(Узел, НомерПринятогоСообщения);
	
	ДанныеДляОтправки = Новый Массив;
	
	Пока ВыборкаИзменений.Следующий() Цикл
		
		Объект = ВыборкаИзменений.Получить();
		
		Если ТипЗнч(Объект) = Тип("СправочникОбъект.Номенклатура") Тогда
			ОписаниеОбъекта = ОписаниеНоменклатуры(Объект);
		ИначеЕсли ТипЗнч(Объект) = Тип("СправочникОбъект.Контрагенты") Тогда
			ОписаниеОбъекта = ОписаниеКонтрагента(Объект);
		ИначеЕсли ТипЗнч(Объект) = Тип("СправочникОбъект.ДоговорыКонтрагентов") Тогда
			ОписаниеОбъекта = ОписаниеДоговора(Объект);
		Иначе
			Продолжить;
		КонецЕсли;
		
		ДанныеДляОтправки.Добавить(ОписаниеОбъекта);
		
	КонецЦикла;
	
	Возврат ЗаписатьЗначениеJSON(ДанныеДляОтправки);	
	
КонецФункции

Функция ОписаниеДоговора(Объект)
	
	Результат = Новый Структура;
	Результат.Вставить("Тип", "Справочник.ДоговорыКонтрагентов");
	Результат.Вставить("Идентификатор", Строка(Объект.Ссылка.УникальныйИдентификатор()));
	Результат.Вставить("Наименование", Объект.Наименование);
	Результат.Вставить("Владелец", Строка(Объект.Владелец.УникальныйИдентификатор()));
	
	Возврат Результат;
	
КонецФункции

Функция ОписаниеКонтрагента(Объект)
	
	Результат = Новый Структура;
	Результат.Вставить("Тип", "Справочник.Контрагенты");
	Результат.Вставить("Идентификатор", Строка(Объект.Ссылка.УникальныйИдентификатор()));
	Результат.Вставить("Наименование", Объект.Наименование);
	
	Возврат Результат;
	
КонецФункции

Функция ОписаниеНоменклатуры(Объект)
	
	Результат = Новый Структура;
	Результат.Вставить("Тип", "Справочник.Номенклатура");
	Результат.Вставить("Идентификатор", Строка(Объект.Ссылка.УникальныйИдентификатор()));
	Результат.Вставить("Наименование", Объект.Наименование);
	Результат.Вставить("ЭтоГруппа", Объект.ЭтоГруппа);
	Результат.Вставить("Родитель", Строка(Объект.Родитель.УникальныйИдентификатор()));
	
	Возврат Результат;
	
КонецФункции

Процедура ПервичнаяРегистрация(Узел)
	
	ОбъектыДляРегистрации = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ОбъектыДляРегистрации.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Владелец КАК Контрагент,
	|	ДоговорыКонтрагентов.Ссылка КАК Договор
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидДоговора.СПокупателем)
	|ИТОГИ
	|ПО
	|	Владелец";
	
	ВыборкаКонтрагенты = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаКонтрагенты.Следующий() Цикл
		ОбъектыДляРегистрации.Добавить(ВыборкаКонтрагенты.Контрагент);
		ВыборкаДоговоры = ВыборкаКонтрагенты.Выбрать();
		Пока ВыборкаДоговоры.Следующий() Цикл
			ОбъектыДляРегистрации.Добавить(ВыборкаДоговоры.Договор);
		КонецЦикла;			
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ОбъектыДляРегистрации) Тогда
		ПланыОбмена.ЗарегистрироватьИзменения(Узел, ОбъектыДляРегистрации);
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти
