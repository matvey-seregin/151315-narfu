
#Область ПрограммныйИнтерфейс

// Проверяет, что дата документа лежит между датой начала и датой окончания договора.
// 
// Параметры:
//  ДокументОбъект - ДокументОбъект - Документ объект
//  Отказ - Булево
Процедура ПроверитьДоговорВДокументе(ДокументОбъект, Отказ) Экспорт
	
	ДоговорКонтрагента = ДокументОбъект.ДоговорКонтрагента;
	Дата = ДокументОбъект.Дата;
	
	Если Не ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДоговорыКонтрагентов.ДатаНачала КАК ДатаНачала,
		|	ДоговорыКонтрагентов.ДатаОкончания КАК ДатаОкончания
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	ДоговорыКонтрагентов.Ссылка = &Ссылка";
		
	Запрос.УстановитьПараметр("Ссылка", ДоговорКонтрагента);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Ошибка при получении реквизитов договора";
		Сообщение.УстановитьДанные(ДокументОбъект);
		Сообщение.Поле = "ДоговорКонтрагента";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если Дата < Выборка.ДатаНачала Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Дата документа не должна быть меньше даты начала действия договора";
		Сообщение.УстановитьДанные(ДокументОбъект);
		Сообщение.Поле = "ДоговорКонтрагента";
		Сообщение.Сообщить();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Выборка.ДатаОкончания) И Дата > Выборка.ДатаОкончания Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Дата документа не должна быть больше даты окончания договора";
		Сообщение.УстановитьДанные(ДокументОбъект);
		Сообщение.Поле = "ДоговорКонтрагента";
		Сообщение.Сообщить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти